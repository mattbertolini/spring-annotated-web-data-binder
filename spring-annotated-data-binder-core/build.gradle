plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

dependencies {
    api 'org.springframework:spring-context'
    api 'org.springframework:spring-beans'
    api 'org.springframework:spring-web'
    compileOnly 'com.google.code.findbugs:jsr305'

    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.springframework:spring-test'
    testImplementation 'nl.jqno.equalsverifier:equalsverifier'
}

javadoc {
    options {
        links(rootProject.ext.javadocLinks)
        addStringOption('Xdoclint:none', '-quiet')
    }
}

jar {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': archiveVersion,
            'Automatic-Module-Name': 'com.mattbertolini.spring.web.bind'
        )
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

test.finalizedBy jacocoTestReport

projectsWithCoverage << project

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username findProperty('sonatype.ossrh.username')
                password findProperty('sonatype.ossrh.password')
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java

            pom {
                name = 'Spring Annotated Data Binder Core'
                description = 'Core module for Spring annotated web data binder'
                url = 'https://github.com/mattbertolini/spring-annotated-web-data-binder'
                developers {
                    developer {
                        name = 'Matt Bertolini'
                    }
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/mattbertolini/spring-annotated-web-data-binder.git'
                    developerConnection = 'scm:git:ssh://github.com/mattbertolini/spring-annotated-web-data-binder.git'
                    url = 'https://github.com/mattbertolini/spring-annotated-web-data-binder'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
