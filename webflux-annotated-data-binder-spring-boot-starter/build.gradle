plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    api project(':spring-webflux-annotated-data-binder')
    implementation 'org.springframework.boot:spring-boot-autoconfigure:2.3.4.RELEASE'
}

jar {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': archiveVersion,
            'Automatic-Module-Name': 'com.mattbertolini.spring.web.reactive.bind.autoconfigure'
        )
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

test.finalizedBy jacocoTestReport

projectsWithCoverage << project

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username findProperty('sonatype.ossrh.username')
                password findProperty('sonatype.ossrh.password')
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java

            pom {
                name = 'Spring WebFlux Annotated Data Binder Spring Boot Starter'
                description = 'Spring Boot starter for Spring WebFlux Annotated Java Bean data binder'
                url = 'https://github.com/mattbertolini/spring-annotated-web-data-binder'
                developers {
                    developer {
                        name = 'Matt Bertolini'
                    }
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/mattbertolini/spring-annotated-web-data-binder.git'
                    developerConnection = 'scm:git:ssh://github.com/mattbertolini/spring-annotated-web-data-binder.git'
                    url = 'https://github.com/mattbertolini/spring-annotated-web-data-binder'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
