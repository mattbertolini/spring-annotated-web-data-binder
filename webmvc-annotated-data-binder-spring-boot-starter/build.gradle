plugins {
    id 'java-library'
    id 'com.mattbertolini.build.maven-central-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    api project(':spring-webmvc-annotated-data-binder')
    api 'org.springframework.boot:spring-boot-starter'

    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.springframework:spring-test'
    testImplementation 'org.springframework.boot:spring-boot-test'
}

jar {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': archiveVersion,
            'Automatic-Module-Name': 'com.mattbertolini.spring.web.mvc.bind.autoconfigure'
        )
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

mavenCentralPublish {
    name = "Spring MVC Annotated Data Binder Spring Boot Starter"
    description = "Spring Boot starter for Spring MVC Annotated Java Bean data binder"
}
